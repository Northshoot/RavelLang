streaming model Ping2():
    properties:
        records = 5
        flow = EmbeddedSpace -> GatewaySpace
    schema:
        value : int32

local model Reference():
    properties:
        records = 1
        durable = True
    schema:
        value : int32

streaming model Pong():
    properties:
        records = 5
        flow = GatewaySpace -> EmbeddedSpace
    schema:
        value : int32


controller PingController(ref : Reference, ping2 : Ping2, pong : Pong):
    event system.started():
        system.print("system started")
        refrec : Reference.Record
        if ref.size() == 1:
            refrec = ref.get(0)
        else:
            refrec = ref.create()
            refrec.value = 42
            ref.save(refrec)
        pingrec = ping2.create()
        pingrec.value = refrec.value
        ping2.save(pingrec)

        for refrec in ref:
            system.print("one record")

    event pong.arrived():
        system.print("pong received")
        pongrec = self.record
        refrec = ref.get(0)
        if pongrec.value != refrec.value:
            system.print("uh oh! the values don't match")
            system.print_number(" want :", refrec.value)
            system.print_number(" Got :", pongrec.value)
        else:
            system.print("pong ok")
            pong.delete(self.record)
            system.print("sending ping2")
            refrec.value += 1
            ref.save(refrec)
            pingrec = ping2.create()
            pingrec.value = refrec.value
            ping2.save(pingrec)

    event ping2.departed():
        system.print("ping2 departed")

    event ping2.save_done():
        system.print("ping2 save done")
        ping2.delete(self.record)


controller PongController(ping2 : Ping2, pong : Pong):
    event ping2.arrived():
        system.print("ping2 received")
        pingrec = self.record
        pongrec = pong.create()
        x = pingrec.value

        pongrec.value = x
        pong.save(pongrec)
        ping2.delete(self.record)

    event pong.departed():
        system.print("pong departed")
        pong.delete(self.record)


space EmbeddedSpace:
    platform:
        language = CLang
        system = Nrf52.v1
        #system = Posix.v2008

    models:
        ref = Reference()
        ping2 = Ping2()
        pong = Pong()

    controllers:
        ping_cnt = PingController(ref = models.ref, ping2 = models.ping2, pong = models.pong)

space GatewaySpace:
    platform:
        language = JLang
        system = Android.v19
        #system = J2SE.v18

    models:
        ping2 = Ping2()
        pong = Pong()

    controllers:
        pong_cnt = PongController(ping2 = models.ping2, pong = models.pong)
